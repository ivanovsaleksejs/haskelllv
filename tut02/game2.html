<html>
<head>
    <meta charset="utf-8">
    <title>Haskell.lv - Rakstām spēli, turpinājums</title>
    <link href="https://fonts.googleapis.com/css?family=Source+Sans+Pro" rel="stylesheet">
    <link rel="stylesheet" type="text/css" href="/index.css">
    <link rel="shortcut icon" href="/favicon.ico" type="image/x-icon">
</head>
<body>
    <div class="header">
        <img src="/logo.png" id="logo" />
        <div class="nav">
           <a href="/" class="active">Apmācība</a>
           <a href="https://www.haskell.org/downloads" target="_blank">Lejupielādēt</a>
           <a href="/irc.html">IRC</a>
           <a href="/links.html">Saites uz citiem resursiem</a>
        </div>
    </div>
    <div class="content">
        <div class="tutorial">
            <div class="wrapper">
                <div class="tutor_nav">
                    <a class="link prev" href="/tut02/game1.html">4.1. Rakstām spēli Haskell valodā</a>
                    <a class="link next" href="/tut02/game3.html">4.3. Sakārtojām spēles kodu</a>
                </div>
                <h1>4.2. Rakstām spēli (turpinājums)</h1>
                <h2>Ievades apstrāde</h2>
                <p>Iepriekšējā sadaļā mēs esam izdarījuši ļoti daudz darba - mēs izveidojām funkciju, kas apkalpo galveno spēles ciklu, izstrādājām likmes, izveidojām palīgfunkcijas likmju apstrādei. Palika pavisam maz - izveidot funkcijas, kas uzsāk spēli, apstrādā likmi, ģenerē un apstrādā rezultātu. Kad mēs to pabeigsim, mums būs strādājoša spēle, kuru mēs nedaudz sakārtosim.</p>
                <p>Sāksim ar funkciju, kas apstrādā lietotāja ievadi. Tai ir jāparedz visas darbības, ko lietotājs var izdarīt, un jāparedz kļūdas. Iepriekšējā sadaļā mēs šo funkciju nosaucām par <span class="grey">processInput</span> un noteicām, ka tai ir trīs argumenti - likmes izmērs, ievadīta rinda, un naudas daudzums. Funkcijas nosaukums ar argumentiem izskatīsies šādi:</p>
                <pre>processInput betSize bet money</pre>
                <p>Visas darbības apstrādāsim ar guard (|) simboliem. Pirmā iespējama darbība būs iziešana no spēles. Mēs izveidosim atsevišķu funkciju pārbaudei, jo mēs to varēsim izmantot citur:</p>
                <pre>quit s = any (`isPrefixOf` s) [":q", "q", "exit"]</pre>
                <p>Pieliksim šo pārbaudi processInput funkcijā:</p>
                <pre>    | quit input = end "quit."</pre>
                <p>Lai nomainītu likmi, mēs izmantosim atslēgvārdu "change". Ja lietotājs ievadīs "change 3", viņa likme tiks samainīta uz 3 un paliks tāda, kamēr viņš nenomainīs likmi vēlreiz, vai kamēr viņa nauda nebūs mazāk par 3. Pieliekam pārbaudi:</p>
                <pre>    | isPrefixOf "change" input = changeValue</pre>
                <p>Funkciju changeValue izveidosim zem <span class="grey">where</span> atslēgvārda, jo mēs neplānosim to izmantot kaut kur citur:</p>
                <pre>    where
        changeValue
            | validValue = game newValNum money
            | otherwise  = wrongInput
            where
                newVal     = dropWhile (== ' ') $ dropWhile (/= ' ') input
                newValNum  = read newVal :: Int
                validValue = isNumber newVal && newValNum >= 1 && newValNum <= money</pre>
                <p>Konstrukcija <span class="grey">dropWhile (== ' ') $ dropWhile (/= ' ') input</span> paņem rindu, piemēram "change 3", izdzēš visus simbolus līdz atstarpei un tad noņem visas atstarpes. Paliks tikai 3. Pēc tam, mēs pārbaudām, vai tas, kas palika, ir skaitlis, vai tas skaitlis ir lielāks par nulli un ir mazāk vai vienāds ar naudas summu. Ja viss ir kārtībā, tad mēs izpildām galveno spēles funkciju ar jauno likmes izmēru. Ja ir notikusi kļūda, tad izpildām funkciju wrongInput, kuru aprakstīsim turpat zem where:</p>
                <pre>        wrongInput = do
            putStrLn "Wrong input!"
            game betSize money</pre>
                <hr />
                <p>Nākošais noteikums mūsu <span class="grey">processInput</span> funkcijā izpildīsies, ja ir ievadīta pareiza likme. Mums vajag izveidot divas papildfunkcijas zem tā paša <span class="grey">where</span>, kuras pārbaudīs, vai likmes ir pareizas. Pie tam, mums ir svarīgi atdalīt šīs funkcijas, jo no likmes ir atkarīgs uzvaras vai zaudējuma koeficients. Funkcijas izskatīsies šādi:</p>
                <pre>        simpleBet = inList input bets
        numberBet = checkNumbers input && checkList input numberBets</pre>
                <p>Mēs izmantojam palīgfunkcijas no iepriekšajas sadaļas. Ko dara šīs divas funkcijas saprast nav grūti. Funkcija <span class="grey">simpleBet</span> atgriež true, ja likme ir starp definētām likmēm. Savukārt, <span class="grey">numbersBet</span> pārbauda, vai ievads atbilst formātam "skaitļi sadalīti ar komatu", ka skaitļu skaits ir pareizs un visi skaitļi ir starp 0 un 36. Ja viena no pārbaudēm tiks izieta, mums vajadzēs izsaukt funkciju, kas ģenerēs skaitli un apstrādās rezultātu. Sākumā mums vajag izdomāt, kādi argumenti būs vajadzīgi šai funkcijai.</p>
                <p>Pirmais arguments var būt likmes izmērs - tas ir vajadzīgs uzvaras vai zaudējuma aprēķinos. Otrais arguments ir mūsu nauda. Kā trešo argumentu izmantosim sarakstu ar skaitļiem, kuri uzvar ar esošo likmi. Definēto likmju gadījumā šis saraksts ir bet saraksta pāru otrais elements, bet skaitļu likmju gadījumā tie ir paši skaitļi. Noteiksim to zem <span class="grey">where</span>:</p>
                <pre>       winners
            | simpleBet = tripleLookup input bets snd3 []
            | numberBet = toNumbers input</pre>
            <p>Pievērsiet uzmanību, kā mēs padodam <span class="grey">snd3</span> funkciju, lai iegūtu otro pāra elementu.</p>
            <p>Vēl mums vajag padot likmes koeficientu, kurš definētām likmēm ir bets sarakstā pāru trešais elements, savukārt, skaitļu likmēm tas vienmēr ir 36. Noteiksim to turpat zem <span class="grey">where</span>:</p>
                <pre>        quotient
            | simpleBet = tripleLookup input bets thd3 0
            | otherwise = 36</pre>
                <p>Un pēdējais argiments ir reizinātājs, ar kuru mēs zināsim, cik daudz ir uzlikts, tas ir, cik ir jāatņem no spēlētāja naudas pirms rēķināt rezultātu. Definētam likmēm tas vienmēr ir viens, bet skaitļu likmēm tas ir tik daudz, uz cik skaitļiem ir uzlikts:</p>
                <pre>        multiplier
            | simpleBet = 1
            | otherwise = length $ toNumbers input</pre>
                <p>Tagad mēs varam pabeigt mūsu <span class="grey">processInput</span> funkciju. Tā sanāks diezgan liela, tāpēc mēs izmantosim dažas atkāpes, lai uzlabotu lasāmību. Pie reizes izveidosim dažus mainīgus izteiksmēm, kas atkārtojas, piemēram <span class="grey">toNumbers input</span>. Kods sanāks šāds:</p>
                <pre>processInput betSize input money
    | quit input             = end "quit."
    | change                 = changeValue
    | simpleBet || numberBet = generateNumber betSize money winners quotient multiplier
    | otherwise              = wrongInput
    where
        change    = isPrefixOf "change" input

        changeValue
            | validValue = game newValNum money
            | otherwise  = wrongInput
            where
                newVal     = dropWhile (== ' ') $ dropWhile (/= ' ') input
                newValNum  = read newVal :: Int
                validValue = isNumber newVal && newValNum >= 1 && newValNum <= money

        wrongInput = do
            putStrLn "Wrong input!"
            game betSize money

        simpleBet = inList input bets
        numberBet = checkNumbers input && checkList input numberBets

        numbers  = toNumbers input
        winners
            | simpleBet = tripleLookup input bets snd3 []
            | numberBet = numbers

        quotient
            | simpleBet = tripleLookup input bets thd3 0
            | otherwise = 36

        multiplier
            | simpleBet = 1
            | otherwise = length numbers</pre>
                <hr />
                <h2>Skaitļa ģenerācija un rezultāta apstrāde</h2>
                <p>Tagad mums vajag uzrakstīt funkciju, kas ģenerē skaitli un apstrādā rezultātu. Mēs jau zinām, kā funkcija sauksies un kādi tai būs argumenti. Pāriesim pie paša skaitļa ģenerēšanas. Lai saģenerētu gadījuma skaitli, Haskell valodā ir vairākas iespējas, bet mēs izmantosim funkciju <span class="grey">randomIO</span> no bibliotēkas <span class="grey">System.Random</span>. Tā funkcija ģenerē skaitli, kuru ierobežo tipa intervāls un kā sākotnēja vērtība tiek izmantots globālais sistēmas ģenerators.</p>
                <pre>randomIO :: Random a => IO a</pre>
                <p>Kā redzat, rezultāts ir <span class="grey">IO</span> kontekstā. Mēs gribām dabūt skaitli, kas nav lielāks par 36, tāpēc mums vajag atrast atlikumu no saģenerēta skaitļa sadalīta ar 37. Izmantosim funkciju <span class="grey">mod</span>, un tā, ka skaitlis ir <span class="grey">IO</span> konteksta, mums vajag izmantot <span class="grey">fmap</span>:</p>
                <pre>generateNumber betSize money winners quotient multiplier = do
    gen <- fmap (`mod` 37) randomIO
    putStr $ "Number won: " ++ (show gen) ++ " "</pre>
                <p>Vēl attēlosim nedaudz informācijas par skaitli - krāsa un paritāte:</p>
                <pre>    if gen == 0
        then putStrLn ""
        else do
            if elem gen reds then putStr "red" else putStr "black"
            if odd gen then putStrLn " odd" else putStrLn " even"</pre>
                <p>Tagad atlicis tikai noteikt, vai spēlētājs ir uzvarējis, un aprēķināt rezultātu. Tās darbības ir vienkāršās, tāpēc mēs uzreiz apskatīsimies visu funkciju:</p>
                <pre>generateNumber betSize money winners quotient multiplier = do
    gen <- fmap (`mod` 37) randomIO
    putStr $ "Number won: " ++ (show gen) ++ " "

    if gen == 0
        then putStrLn ""
        else do
            if elem gen reds then putStr "red" else putStr "black"
            if odd gen then putStrLn " odd" else putStrLn " even"

    if elem gen winners
        then do
            let w = (quotient - multiplier) * betSize
            putStrLn $ "You won " ++ (show w)
            game betSize $ money + w
        else do
            let l = betSize * multiplier
            putStrLn "You lost "
            game betSize $ money - l</pre>
                <p>Mēs redzam kaut ko, ko mēs esam izmantojuši GHCi, bet neesam izmantojuši kodā - <span class="grey">let</span> atslēgvārdu. Atcerēsimies, ko tas dara - tas ļauj veidot definīcijas izteiksmēs. Var saprast, ka katra rinda do blokā ir izteiksme, kurai ir noteikts rezultāts, tāpēc tajās nevar definēt funkcijas vai datus, bez <span class="grey">let</span> izteiksmes.</p>
                <hr />
                <h2>Spēles kods</h2>
                <p>Mums vajag vēl izveidot funkciju, kas uzsāk spēli, pieprasa no spēlētāja ievadīt naudas summu un izsauc game funkciju, vai kļūdas gadījumā attēlos paziņojumu un izsauks pati sevi vēlreiz. Funkcija sauksies <span class="grey">gameStart</span>:</p>
                <pre>-- Main cycle
gameStart = do
    putStrLn "Enter amount"
    m <- getLine
    if quit m
        then putStrLn "Bye!"
        else do
            if isNumber m
                then game 1 $ read m
                else do
                    putStrLn "Wrong input!"
                    gameStart</pre>
                <p>Izdarīsim vēl vienu sīkumu, lai mūsu spēle būtu nedaudz lietojamāka - pieliksim krāsas dažiem paziņojumiem. Lai konsolē attēlotu simbolus ar krāsu, tos vajag ieslēgt speciālos simbolos, piemēram, kods <span class="grey">putStrLn $ "\x1b[31m" ++ "You lost " ++ "\x1b[0m"</span> attēlos rindu <span class="grey">You lost</span> sarkanajā krāsā. Uztaisīsim vienu funkciju, kas attēlos krāsainas rindas, un vēl trīs papildfunkcijas sarkanai, zaļai un rozā krāsām:</p>
                <pre>colorLine color line = "\x1b[" ++ color ++ "m" ++ line ++ "\x1b[0m"

redLine   = colorLine "31"
greenLine = colorLine "32"
pinkLine  = colorLine "35"</pre>
                <p>Jautājums, vai var kaut kā vēl optimizēt, lai nebūtu jāatkārto funkcija <span class="grey">colorLine</span> trīs reizes. Var aizvietot tās trīs rindas ar vienu:</p>
                <pre>[redLine, greenLine, pinkLine] = map colorLine ["31", "32", "35"]</pre>
                <p>Pamēģiniet šo koda gabalu saprast paši.</p>
                <p>Saliksim mūsu funkcijas spēlē. Funkcijā gameStart:</p>
                <pre>putStrLn $ redLine "Wrong input!"</pre>
                <p>Funkcijā game:</p>
                <pre>end $ redLine "You lost!"
...
putStrLn $ pinkLine $ "Money too low, bet value changed to " ++ (show money) ++ "!"
...
putStrLn $ pinkLine $ "Money: " ++ (show money) ++ ". Bet value: " ++ (show betSize)</pre>
                <p>Funkcijā processInput:</p>
                <pre>putStrLn $ redLine "Wrong input!"</pre>
                <p>Funkcijā generateNumber:</p>
                <pre>if elem gen reds then putStr (redLine "red") else putStr "black"
...
putStrLn $ greenLine $ "You won " ++ (show w)
...
putStrLn $ redLine "You lost"</pre>
                <hr />
                <p>Mūsu spēles kods ir gatavs. Varam to apskatīties pilnībā:</p>
                <pre>import Data.Char hiding (isNumber)
import Data.List
import Data.Tuple.Utils

import System.Random

main = gameStart

-- Main cycle
gameStart = do
    putStrLn "Enter amount"
    m <- getLine
    if quit m
        then putStrLn "Bye!"
        else do
            if isNumber m
                then game 1 $ read m
                else do
                    putStrLn $ redLine "Wrong input!"
                    gameStart

end s = putStrLn $ "Game ended: " ++ s

quit s = any (`isPrefixOf` s) [":q", "q", "exit"]

game betSize money
    | money <= 0      = end $ redLine "You lost!"
    | betSize > money = do
        putStrLn $ pinkLine $ "Money too low, bet value changed to " ++ (show money) ++ "!"
        game money money
    | otherwise       = do
        putStrLn $ pinkLine $ "Money: " ++ (show money) ++ ". Bet value: " ++ (show betSize)
        putStrLn "Enter your bet (to change amount of bet enter 'change &lt;number&gt;')"
        input <- getLine
        processInput betSize input money


processInput betSize input money
    | quit input             = end "quit."
    | change                 = changeValue
    | simpleBet || numberBet = generateNumber betSize money winners quotient multiplier
    | otherwise              = wrongInput
    where
        change    = isPrefixOf "change" input

        changeValue
            | validValue = game newValNum money
            | otherwise  = wrongInput
            where
                newVal     = dropWhile (== ' ') $ dropWhile (/= ' ') input
                newValNum  = read newVal :: Int
                validValue = isNumber newVal && newValNum >= 1 && newValNum <= money

        wrongInput = do
            putStrLn $ redLine "Wrong input!"
            game betSize money

        simpleBet = inList input bets
        numberBet = checkNumbers input && checkList input numberBets

        numbers  = toNumbers input
        winners
            | simpleBet = tripleLookup input bets snd3 []
            | numberBet = numbers

        quotient
            | simpleBet = tripleLookup input bets thd3 0
            | otherwise = 36

        multiplier
            | simpleBet = 1
            | otherwise = length numbers


generateNumber betSize money winners quotient multiplier = do
    gen <- fmap (`mod` 37) randomIO
    putStr $ "Number won: " ++ (show gen) ++ " "

    if gen == 0
        then putStrLn ""
        else do
            if elem gen reds then putStr (redLine "red") else putStr "black"
            if odd gen then putStrLn " odd" else putStrLn " even"

    if elem gen winners
        then do
            let w = (quotient - multiplier) * betSize
            putStrLn $ greenLine $ "You won " ++ (show w)
            game betSize $ money + w
        else do
            let l = betSize * multiplier
            putStrLn $ redLine "You lost"
            game betSize $ money - l


-- Helpers

-- Check if all symbols in string are digits
isNumber n = n /= "" && all isDigit n

-- Check if string is numbers separated by comma
checkNumbers = all isNumber . splitBy ','

-- Split string by comma and convert to Int
toNumbers = nub . map (\x -> read x :: Int) . splitBy ','

-- Split string by delimiter
splitBy delimiter = foldr checkDelimiter [[]]
    where
        checkDelimiter c l@(x:xs)
            | c == delimiter = [] : l
            | otherwise      = (c:x) : xs

-- Check if number count is within allowed lengths and all numbers are in [0..36]
checkList l lengths = (length parsedList) `elem` lengths && all (`elem` [0..36]) parsedList
    where parsedList = toNumbers l

-- Check if argument is index in list of triples
inList :: Eq a => a -> [(a, b, c)] -> Bool
inList index = any (\ x -> index == fst3 x)

-- Find index in list of triples and execute function on that triple
tripleLookup _ [] _ def = def
tripleLookup index (l:ls) fn def
    | fst3 l == index = fn l
    | otherwise       = tripleLookup index ls fn def

colorLine color line = "\x1b[" ++ color ++ "m" ++ line ++ "\x1b[0m"

[redLine, greenLine, pinkLine] = map colorLine ["31", "32", "35"]

-- Bets
reds   = [1,3,5,7,9,12,14,16,18,19,21,23,25,27,30,32,34,36]
blacks = [1..36] \\ reds

bets = [
        ("odd",    [1,3..35],  2),
        ("even",   [2, 4..36], 2),
        ("red",    reds,       2),
        ("black",  blacks,     2),
        ("1..18",  [1..18],    2),
        ("19..36", [19..36],   2),
        ("1..12",  [1..12],    3),
        ("13..24", [13..24],   3),
        ("25..36", [25..36],   3)
    ]

numberBets = [1,3,6]</pre>
                <p>Šo kodu var nokompilēt, un tas strādās pilnvērtīgi. Koda trūkums ir ka viss ir samests vienā failā. Nākamajā sadaļā mēs mēģināsim atrisināt šo problēmu.</p>
                <br />
                <br />
                <a class="link" href="/tut02/game3.html"><h3>>> 4.3. Sakārtojām spēles kodu</h3></a>
                <br />
                <br />
                <br />
            </div>
        </div>
    </div>
    <script type="text/javascript" src="/main.js"></script>
    <script type="text/javascript" src="/spoiler.js"></script>
</body>
</html>
